buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
  }
}

plugins {
  id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
  mavenCentral()
}

def clientVersion = '0.6.0'
def grpcVersion = '1.2.0'
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

dependencies {
  compile "io.grpc:grpc-netty:${grpcVersion}"
  compile "io.grpc:grpc-protobuf:${grpcVersion}"
  compile "io.grpc:grpc-stub:${grpcVersion}"
  compile 'com.orbitz.consul:consul-client:0.13.11'
  
  testCompile 'junit:junit:4.12'
  testCompile 'com.jayway.awaitility:awaitility:1.7.0'
  testCompile 'com.pszymczyk.consul:embedded-consul:0.2.0'
}

shadowJar {
  baseName = 'com.cleo.clarify.control.client'
  version = clientVersion
  mergeServiceFiles()
  // Server has multiple versions of guava so we're going to hide this one
  relocate 'com.google.common', 'internal.com.google.common'
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.2.0'
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {
        option 'enable_deprecated=false'
      }
    }
  }
}